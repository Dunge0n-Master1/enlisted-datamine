require ecs
require soundEvent
require soundSystem
require Dacoll
require enlisted.events_enlisted

[es(tag=sound, no_order)]
def underwater_sound(info : ParallelUpdateFrameDelayed;
                     underwater_sound__path : string;
                     var underwater_sound__event : SoundEvent&;
                     var underwater_sound__frame : int&;
                     underwater_sound__aboveWaterThreshold = 0.1)
  ++underwater_sound__frame
  if (underwater_sound__frame & 3) == 0
    var waterLevel = 0.0
    let pos = get_listener_pos()
    if traceht_water(pos, waterLevel)
      if underwater_sound__event.enabled && pos.y - waterLevel > underwater_sound__aboveWaterThreshold
        underwater_sound__event.enabled = false
        abandon(underwater_sound__event.handle)
      elif !underwater_sound__event.enabled && pos.y < waterLevel
        underwater_sound__event.enabled = true
        abandon(underwater_sound__event.handle)
        underwater_sound__event |> play(underwater_sound__path)
  if underwater_sound__event.enabled
    set_pos(underwater_sound__event.handle, get_listener_pos())
